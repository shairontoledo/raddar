# Notifications

window.add_message_notification = (item) ->
  menu = $('li#messages_notifications').children('ul.dropdown-menu')
  menu.children("li.#{item.user}_message_notification").remove()
  menu.prepend item.content
  update_notifications_count 'messages_notifications'


update_notifications_count = (id) ->
  notification_count = $("li##{id}").find("span.notification_count")
  icon = $("li##{id}").find("i")
  notification_count.html $("li##{id}").children('ul.dropdown-menu').children('li.notification').size()

  if parseInt(notification_count.html()) > 0
    notification_count.show()
    icon.addClass 'icon-white'
  else
    notification_count.hide()
    icon.removeClass 'icon-white'

build_notifications_count = (id, read_url)->
  update_notifications_count id
  $("li##{id}").find('a.dropdown-toggle').on 'click', (e)->
    if parseInt($("li##{id}").find("span.notification_count").html()) > 0
      $.ajax
        type: 'POST'
        url: read_url


# Confirm on unload window 

confirm_form_changed = false

window.confirm_unload = ->
  confirm_form_changed = true
  
  
check_confirm_unload = ->  
  if confirm_form_changed  
    return "<%= I18n.t 'js.confirm_unload' %>";  


# Maps and geolocation

window.single_marker_map =  (map_id, location, getCoordinates) ->

  myOptions =
    zoom: 15
    center: location
    mapTypeId: google.maps.MapTypeId.ROADMAP

  map = new google.maps.Map(document.getElementById(map_id), myOptions)

  marker = new google.maps.Marker
    map: map
    position: location
    animation: google.maps.Animation.DROP
  if getCoordinates
    marker.setDraggable true
    google.maps.event.addListener marker, 'dragend', ->
      $('#lat').val marker.getPosition().lat()
      $('#lng').val marker.getPosition().lng()


window.simple_map =  (map_id, centerLocation) ->

  myOptions =
    zoom: 15
    center: centerLocation
    mapTypeId: google.maps.MapTypeId.ROADMAP

  new google.maps.Map(document.getElementById(map_id), myOptions)


window.user_location = (callback)->
  handleNoGeolocation = (error)->
    console.log "Geolocation service failed."
    callback null

  if navigator.geolocation
    navigator.geolocation.getCurrentPosition (position) ->
      callback
        lat: position.coords.latitude
        lng: position.coords.longitude
    , handleNoGeolocation
  else
    handleNoGeolocation(null)
  
  

$ ->

  build_notifications_count 'messages_notifications', '/messages/read'
  build_notifications_count 'general_notifications', '/notifications/read'
  window.onbeforeunload = check_confirm_unload
